root = { stmt } , [ expr ] ;
stmt = expr , ";" ;
expr = binding
     | declaration ;
binding = alternative , ":=" , binary
        | alternative , ":" binary , "=" , binary ;
declaration = alternative , ":" , binary
            | binary ;
binary = index , index , application
       | application ;
application = alternative , argument , { "," , argument }
            | argument ;
argument = alternative , ":" , function
         | function ;
function = index , "=>" , binary | index ;
index = index , "'" , dotted
      | dotted ;
dotted = dotted , "." , alternative
       | alternative ;
alternative = "$" , atomic
            | atomic ;
atomic = "(" , [ declaration , { ";" , declaration } ] , ")"
       | "[" , [ declaration , { ";" , declaration } ] , "]"
       | "{" , root , "}"
       | ?integer?
       | ?real?
       | ?identifier?
       | ?string? ;

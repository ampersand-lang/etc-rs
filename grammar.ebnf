root = { stmt } , [ expr ] ;
stmt = expr , ";" ;
expr = binding
     | declaration ;
binding = reader , ":=" , binary
        | reader , ":" binary , "=" , binary
        | { attribute } , reader , "::" , binary
        | { attribute } , reader , ":" binary , ":" , binary
        | ?ident? , "=" , binary ;
attribute = "@" , "(" , ?ident? , [ argument , { "," , argument } ] , ")"
          | "@" , ?ident? ;
declaration = reader , ":" , binary
            | binary ;
with-handler = binary , { "with!" , argument }
binary = index , index , application
       | application ;
application = reader , argument , { "," , argument }
            | argument ;
argument = reader , ":" , function
         | function ;
function = index , "=>" , binary | index ;
index = index , "'" , dotted
      | dotted ;
dotted = dotted , "." , reader
       | reader ;
reader = ?reader macro? , reader
       | alternative ;
alternative = "$" , atomic
            | atomic ;
atomic = "(" , [ declaration , { ";" , declaration } ] , ")"
       | "[" , [ declaration , { ";" , declaration } ] , "]"
       | "{" , root , "}"
       | ?integer?
       | ?real?
       | ?identifier?
       | ?string? ;
